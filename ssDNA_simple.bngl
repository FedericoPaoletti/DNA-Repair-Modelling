begin parameters

	k_RAD51_on	1
	k_RAD51_off	1
	k_BRCA2_on	1
	k_BRCA2_off	1
	kpp_on	1		#on rate for RAD51/BRCA2 protein-protein interaction
	kpp_off	1		#off rate for RAD51/BRCA2 protein-protein interaction
	ssDNA_start 100
	BRCA2_start	100
	RAD51_start	100
	
end parameters

begin molecule types

	ssDNA(b,b)
	BRCA2(b,c)
	RAD51(b,c)
	
end molecule types

begin seed species

	ssDNA(b,b)			ssDNA_start
	BRCA2(b,c)			BRCA2_start
	RAD51(b,c)			RAD51_start
	
end seed species

begin reaction rules

	RAD51(b,c) + BRCA2(b,c) <-> RAD51(b,c!1).BRCA2(b,c!1)		kpp_on, kpp_off #RAD51 and BRCA2 can associate also when not bound to ssDNA!

	RAD51(b,c) + ssDNA(b) -> RAD51(b!1,c).ssDNA(b!1)		k_RAD51_on
	RAD51(b!1,c).ssDNA(b!1) -> RAD51(b,c) + ssDNA(b)		k_RAD51_off  
	
	BRCA2(b,c) + ssDNA(b) -> BRCA2(b!1,c).ssDNA(b!1)		k_BRCA2_on
	BRCA2(b!1,c).ssDNA(b!1) -> BRCA2(b,c) + ssDNA(b)		k_BRCA2_off
	
	ssDNA(b!1,b!2).RAD51(b!1,c).BRCA2(b!2,c) -> ssDNA(b!1,b!2).RAD51(b!1,c!3).BRCA2(b!2,c!3)		kpp_on
	ssDNA(b!1,b!2).RAD51(b!1,c!3).BRCA2(b!2,c!3) -> ssDNA(b!1,b!2).RAD51(b!1,c).BRCA2(b!2,c)		kpp_off
	
	ssDNA(b!1,b!2).RAD51(b!1,c!3).BRCA2(b!2,c!3) -> ssDNA(b!1,b).RAD51(b!1,c!3).BRCA2(b,c!3)		k_BRCA2_off	#Dissociation reaction of BRCA2 from ssDNA (RAD51 still bound to DNA)
	ssDNA(b!1,b!2).RAD51(b!1,c!3).BRCA2(b!2,c!3) -> ssDNA(b,b!2).RAD51(b,c!3).BRCA2(b!2,c!3)		k_RAD51_off #Dissociation reaction of RAD51 from ssDNA (BRCA2 still bound to DNA)
	
	ssDNA(b,b!2).RAD51(b,c!3).BRCA2(b!2,c!3) -> ssDNA(b,b) + RAD51(b,c!3).BRCA2(b,c!3)				k_BRCA2_off #Dissociation reaction of BRCA2 from ssDNA (RAD51 not bound to ssDNA)
	ssDNA(b!1,b).RAD51(b!1,c!3).BRCA2(b,c!3) -> ssDNA(b,b) + RAD51(b,c!3).BRCA2(b,c!3)				k_RAD51_off #Dissociation reaction of RAD51 from ssDNA (BRCA2 not bound to ssDNA)
	
	ssDNA(b,b) + RAD51(b,c!1).BRCA2(b,c!1) -> ssDNA(b,b!1).RAD51(b,c!2).BRCA2(b!1,c!2)				k_BRCA2_on
	ssDNA(b,b) + RAD51(b,c!1).BRCA2(b,c!1) -> ssDNA(b,b!1).RAD51(b!1,c!2).BRCA2(b,c!2)				k_RAD51_on
	
	
end reaction rules

begin observables

	Molecules complex1 ssDNA(b!1,b!2).RAD51(b!1,c).BRCA2(b!2,c)
	Molecules complex2 ssDNA(b!1,b!2).RAD51(b!1,c!3).BRCA2(b!2,c!3)
	Molecules Bound_Rad51 RAD51(b!+)
	Molecules Bound_Brca2 BRCA2(b!+)
	
end observables
	
#actions
generate_network({overwrite=>1})
writeMfile({})
	
	 
